/* 
=========================================================
   DB - Diseño y Desarrollo - Grupo #5
========================================================= 
*/

-------------------------
-- LIMPIEZA Y CREACIÓN
-------------------------
IF EXISTS (SELECT 1 FROM sys.databases WHERE name = 'DBGRUPO5')
BEGIN
    ALTER DATABASE DBGRUPO5 SET SINGLE_USER WITH ROLLBACK IMMEDIATE;
    DROP DATABASE DBGRUPO5;
END
GO

CREATE DATABASE DBGRUPO5;
GO

USE DBGRUPO5;
GO

-------------------------
-- MONTAJE 
-------------------------


/*
=========================================================
   1) TABLA ESTADO 
========================================================= 
*/
CREATE TABLE dbo.ESTADO (
  ID_ESTADO INT NOT NULL PRIMARY KEY,
  NOMBRE NVARCHAR(20) NOT NULL UNIQUE
);
GO

INSERT INTO dbo.ESTADO (ID_ESTADO, NOMBRE) VALUES
(1, N'ACTIVO'),
(2, N'INACTIVO');
GO


/* 
=========================================================
   2) SEGURIDAD
========================================================= 
*/

-- ROLES 
CREATE TABLE dbo.ROLES (
  ID_ROL INT IDENTITY(1,1) NOT NULL PRIMARY KEY,
  NOMBRE NVARCHAR(60) NOT NULL,
  DESCRIPCION NVARCHAR(200) NULL,
  ID_ESTADO INT NOT NULL CONSTRAINT DF_ROLES_ESTADO DEFAULT (1),
  CONSTRAINT FK_ROLES_ESTADO FOREIGN KEY (ID_ESTADO) REFERENCES dbo.ESTADO(ID_ESTADO)
);
GO

INSERT INTO dbo.ROLES (NOMBRE, DESCRIPCION)
VALUES
('ADMINISTRADOR', 'ROL CON ACCESO COMPLETO AL SISTEMA'),
('VENDEDOR',      'ROL ENCARGADO DE VENTAS Y ATENCIÓN AL CLIENTE'),
('CLIENTE',       'ROL PARA CLIENTES DEL SISTEMA');
GO


-- PERMISOS 
CREATE TABLE dbo.PERMISOS (
  ID_PERMISO INT IDENTITY(1,1) NOT NULL PRIMARY KEY,
  NOMBRE NVARCHAR(80) NOT NULL,
  DESCRIPCION NVARCHAR(200) NULL
);
GO

-- ROLES_PERMISOS 
CREATE TABLE dbo.ROLES_PERMISOS (
  ID_ROL INT NOT NULL,
  ID_PERMISO INT NOT NULL,
  CONSTRAINT PK_ROLES_PERMISOS PRIMARY KEY (ID_ROL, ID_PERMISO),
  CONSTRAINT FK_RP_ROL FOREIGN KEY (ID_ROL) REFERENCES dbo.ROLES(ID_ROL),
  CONSTRAINT FK_RP_PER FOREIGN KEY (ID_PERMISO) REFERENCES dbo.PERMISOS(ID_PERMISO)
);
GO

-- USUARIOS 
CREATE TABLE dbo.USUARIOS (
  ID_USUARIO INT IDENTITY(1,1) NOT NULL PRIMARY KEY,
  NOMBRE NVARCHAR(80) NOT NULL,
  CORREO NVARCHAR(120) NOT NULL UNIQUE,
  CONTRASENA NVARCHAR(200) NOT NULL,
  ID_ROL INT NOT NULL,
  ID_ESTADO INT NOT NULL CONSTRAINT DF_USUARIOS_ESTADO DEFAULT (1),
  CONSTRAINT FK_USU_ROL FOREIGN KEY (ID_ROL) REFERENCES dbo.ROLES(ID_ROL),
  CONSTRAINT FK_USU_ESTADO FOREIGN KEY (ID_ESTADO) REFERENCES dbo.ESTADO(ID_ESTADO)
);
GO

-- BITACORA - AUDITORIA
CREATE TABLE dbo.BITACORA (
  ID_BITACORA INT IDENTITY(1,1) NOT NULL PRIMARY KEY,
  ID_USUARIO INT NOT NULL,
  ACCION NVARCHAR(80) NOT NULL,
  DESCRIPCION NVARCHAR(250) NULL,
  FECHA DATETIME2(0) NOT NULL CONSTRAINT DF_BITACORA_FECHA DEFAULT (GETDATE()),
  CONSTRAINT FK_BIT_USU FOREIGN KEY (ID_USUARIO) REFERENCES dbo.USUARIOS(ID_USUARIO)
);
GO


/* 
=========================================================
   3) CLIENTES / PROVEEDORES
========================================================= 
*/

-- CLIENTES 
CREATE TABLE dbo.CLIENTES (
  ID_CLIENTE INT IDENTITY(1,1) NOT NULL PRIMARY KEY,
  NOMBRE NVARCHAR(100) NOT NULL,
  TELEFONO NVARCHAR(30) NULL,
  CORREO NVARCHAR(120) NULL,
  DIRECCION NVARCHAR(200) NULL,
  ID_ESTADO INT NOT NULL CONSTRAINT DF_CLIENTES_ESTADO DEFAULT (1),
  CONSTRAINT FK_CLIENTES_ESTADO FOREIGN KEY (ID_ESTADO) REFERENCES dbo.ESTADO(ID_ESTADO)
);
GO

-- PROVEEDORES 
CREATE TABLE dbo.PROVEEDORES (
  ID_PROVEEDOR INT IDENTITY(1,1) NOT NULL PRIMARY KEY,
  NOMBRE NVARCHAR(120) NOT NULL,
  CONTACTO NVARCHAR(120) NULL,
  TELEFONO NVARCHAR(30) NULL,
  ID_ESTADO INT NOT NULL CONSTRAINT DF_PROVEEDORES_ESTADO DEFAULT (1),
  CONSTRAINT FK_PROVEEDORES_ESTADO FOREIGN KEY (ID_ESTADO) REFERENCES dbo.ESTADO(ID_ESTADO)
);
GO


/* 
=========================================================
   4) INVENTARIO / PRODUCTOS
========================================================= 
*/

-- MATERIALES 
CREATE TABLE dbo.MATERIALES (
  ID_MATERIAL INT IDENTITY(1,1) NOT NULL PRIMARY KEY,
  NOMBRE NVARCHAR(120) NOT NULL,
  TIPO NVARCHAR(60) NOT NULL,
  STOCK DECIMAL(12,2) NOT NULL CONSTRAINT DF_MATERIALES_STOCK DEFAULT (0),
  COSTO_UNITARIO DECIMAL(12,2) NOT NULL CONSTRAINT DF_MATERIALES_COSTO DEFAULT (0),
  ID_PROVEEDOR INT NOT NULL,
  ID_ESTADO INT NOT NULL CONSTRAINT DF_MATERIALES_ESTADO DEFAULT (1),

  CONSTRAINT CK_MAT_STOCK CHECK (STOCK >= 0),
  CONSTRAINT CK_MAT_COSTO CHECK (COSTO_UNITARIO >= 0),
  CONSTRAINT FK_MAT_PROV FOREIGN KEY (ID_PROVEEDOR) REFERENCES dbo.PROVEEDORES(ID_PROVEEDOR),
  CONSTRAINT FK_MAT_ESTADO FOREIGN KEY (ID_ESTADO) REFERENCES dbo.ESTADO(ID_ESTADO)
);
GO

-- CATEGORIAS 
CREATE TABLE dbo.CATEGORIAS (
  ID_CATEGORIA INT IDENTITY(1,1) NOT NULL PRIMARY KEY,
  NOMBRE NVARCHAR(80) NOT NULL,
  DESCRIPCION NVARCHAR(200) NULL,
  ID_ESTADO INT NOT NULL CONSTRAINT DF_CATEGORIAS_ESTADO DEFAULT (1),
  CONSTRAINT FK_CATEGORIAS_ESTADO FOREIGN KEY (ID_ESTADO) REFERENCES dbo.ESTADO(ID_ESTADO)
);
GO

-- PRODUCTOS 
CREATE TABLE dbo.PRODUCTOS (
  ID_PRODUCTO INT IDENTITY(1,1) NOT NULL PRIMARY KEY,
  NOMBRE NVARCHAR(140) NOT NULL,
  PRECIO_VENTA DECIMAL(12,2) NOT NULL CONSTRAINT DF_PRODUCTOS_PRECIO DEFAULT (0),
  ID_CATEGORIA INT NOT NULL,
  ID_ESTADO INT NOT NULL CONSTRAINT DF_PRODUCTOS_ESTADO DEFAULT (1),

  CONSTRAINT CK_PROD_PRECIO CHECK (PRECIO_VENTA >= 0),
  CONSTRAINT FK_PROD_CAT FOREIGN KEY (ID_CATEGORIA) REFERENCES dbo.CATEGORIAS(ID_CATEGORIA),
  CONSTRAINT FK_PROD_ESTADO FOREIGN KEY (ID_ESTADO) REFERENCES dbo.ESTADO(ID_ESTADO)
);
GO

-- PRODUCTO_MATERIAL
CREATE TABLE dbo.PRODUCTO_MATERIAL (
  ID_PRODUCTO INT NOT NULL,
  ID_MATERIAL INT NOT NULL,
  CANTIDAD_USADA DECIMAL(12,2) NOT NULL CONSTRAINT DF_PM_CANT DEFAULT (0),

  CONSTRAINT PK_PRODUCTO_MATERIAL PRIMARY KEY (ID_PRODUCTO, ID_MATERIAL),
  CONSTRAINT CK_PM_CANT CHECK (CANTIDAD_USADA >= 0),
  CONSTRAINT FK_PM_PROD FOREIGN KEY (ID_PRODUCTO) REFERENCES dbo.PRODUCTOS(ID_PRODUCTO),
  CONSTRAINT FK_PM_MAT  FOREIGN KEY (ID_MATERIAL) REFERENCES dbo.MATERIALES(ID_MATERIAL)
);
GO

-- MOVIMIENTOS_INVENTARIO 
CREATE TABLE dbo.MOVIMIENTOS_INVENTARIO (
  ID_MOVIMIENTO INT IDENTITY(1,1) NOT NULL PRIMARY KEY,
  ID_MATERIAL INT NOT NULL,
  TIPO_MOVIMIENTO NVARCHAR(10) NOT NULL, -- REPESENTARIA ENTRADAS Y SALIDAS | TIPO CUANDO LLEGA MATERIAL Y CUANDO SE VENDE
  CANTIDAD DECIMAL(12,2) NOT NULL CONSTRAINT DF_MI_CANT DEFAULT (0),
  FECHA DATETIME2(0) NOT NULL CONSTRAINT DF_MI_FECHA DEFAULT (GETDATE()),
  OBSERVACION NVARCHAR(250) NULL,

  CONSTRAINT CK_MI_TIPO CHECK (TIPO_MOVIMIENTO IN ('ENTRADA','SALIDA')),
  CONSTRAINT CK_MI_CANT CHECK (CANTIDAD >= 0),
  CONSTRAINT FK_MI_MAT FOREIGN KEY (ID_MATERIAL) REFERENCES dbo.MATERIALES(ID_MATERIAL)
);
GO


/* 
=========================================================
   5) VENTAS
========================================================= 
*/

-- VENTAS 
CREATE TABLE dbo.VENTAS (
  ID_VENTA INT IDENTITY(1,1) NOT NULL PRIMARY KEY,
  ID_CLIENTE INT NOT NULL,
  FECHA DATETIME2(0) NOT NULL CONSTRAINT DF_VENTAS_FECHA DEFAULT (GETDATE()),
  TOTAL DECIMAL(12,2) NOT NULL CONSTRAINT DF_VENTAS_TOTAL DEFAULT (0),
  ID_ESTADO INT NOT NULL CONSTRAINT DF_VENTAS_ESTADO DEFAULT (1),

  CONSTRAINT CK_VENTAS_TOTAL CHECK (TOTAL >= 0),
  CONSTRAINT FK_VEN_CLI FOREIGN KEY (ID_CLIENTE) REFERENCES dbo.CLIENTES(ID_CLIENTE),
  CONSTRAINT FK_VEN_ESTADO FOREIGN KEY (ID_ESTADO) REFERENCES dbo.ESTADO(ID_ESTADO)
);
GO

-- DETALLES_VENTAS
CREATE TABLE dbo.DETALLES_VENTAS (
  ID_DETALLE INT IDENTITY(1,1) NOT NULL PRIMARY KEY,
  ID_VENTA INT NOT NULL,
  ID_PRODUCTO INT NOT NULL,
  CANTIDAD DECIMAL(12,2) NOT NULL CONSTRAINT DF_DV_CANT DEFAULT (0),
  PRECIO_UNITARIO DECIMAL(12,2) NOT NULL CONSTRAINT DF_DV_PRECIO DEFAULT (0),
  SUBTOTAL DECIMAL(12,2) NOT NULL CONSTRAINT DF_DV_SUB DEFAULT (0),

  CONSTRAINT CK_DV_CANT CHECK (CANTIDAD >= 0),
  CONSTRAINT CK_DV_PRECIO CHECK (PRECIO_UNITARIO >= 0),
  CONSTRAINT CK_DV_SUB CHECK (SUBTOTAL >= 0),

  CONSTRAINT FK_DV_VENTA FOREIGN KEY (ID_VENTA) REFERENCES dbo.VENTAS(ID_VENTA),
  CONSTRAINT FK_DV_PROD  FOREIGN KEY (ID_PRODUCTO) REFERENCES dbo.PRODUCTOS(ID_PRODUCTO)
);
GO

-- DEVOLUCIONES 
CREATE TABLE dbo.DEVOLUCIONES (
  ID_DEVOLUCION INT IDENTITY(1,1) NOT NULL PRIMARY KEY,
  ID_VENTA INT NOT NULL,
  MOTIVO NVARCHAR(250) NOT NULL,
  FECHA DATETIME2(0) NOT NULL CONSTRAINT DF_DEV_FECHA DEFAULT (GETDATE()),
  CONSTRAINT FK_DEV_VEN FOREIGN KEY (ID_VENTA) REFERENCES dbo.VENTAS(ID_VENTA)
);
GO

-- GARANTIAS 
CREATE TABLE dbo.GARANTIAS (
  ID_GARANTIA INT IDENTITY(1,1) NOT NULL PRIMARY KEY,
  ID_PRODUCTO INT NOT NULL,
  DESCRIPCION NVARCHAR(250) NULL,
  DURACION_MESES INT NOT NULL CONSTRAINT DF_GAR_MESES DEFAULT (0),
  CONDICIONES NVARCHAR(250) NULL,

  CONSTRAINT CK_GAR_MESES CHECK (DURACION_MESES >= 0),
  CONSTRAINT FK_GAR_PROD FOREIGN KEY (ID_PRODUCTO) REFERENCES dbo.PRODUCTOS(ID_PRODUCTO)
);
GO


/* 
=========================================================
   6) MODULO ESTRATÉGICO
========================================================= 
*/

-- CORTES 
CREATE TABLE dbo.CORTES (
  ID_CORTE INT IDENTITY(1,1) NOT NULL PRIMARY KEY,
  NOMBRE NVARCHAR(120) NOT NULL,
  DESCRIPCION NVARCHAR(250) NULL,
  ID_ESTADO INT NOT NULL CONSTRAINT DF_CORTES_ESTADO DEFAULT (1),
  CONSTRAINT FK_CORTES_ESTADO FOREIGN KEY (ID_ESTADO) REFERENCES dbo.ESTADO(ID_ESTADO)
);
GO

-- REGISTRO_CORTES 
CREATE TABLE dbo.REGISTRO_CORTES (
  ID_REGISTRO_CORTE INT IDENTITY(1,1) NOT NULL PRIMARY KEY,
  ID_CORTE INT NOT NULL,
  ID_MATERIAL INT NOT NULL,
  CANTIDAD DECIMAL(12,2) NOT NULL CONSTRAINT DF_RC_CANT DEFAULT (0),
  FECHA DATETIME2(0) NOT NULL CONSTRAINT DF_RC_FECHA DEFAULT (GETDATE()),

  CONSTRAINT CK_RC_CANT CHECK (CANTIDAD >= 0),
  CONSTRAINT FK_RC_CORTE FOREIGN KEY (ID_CORTE) REFERENCES dbo.CORTES(ID_CORTE),
  CONSTRAINT FK_RC_MAT   FOREIGN KEY (ID_MATERIAL) REFERENCES dbo.MATERIALES(ID_MATERIAL)
);
GO

-- DESPERDICIOS_MATERIAL 
CREATE TABLE dbo.DESPERDICIOS_MATERIAL (
  ID_DESPERDICIO INT IDENTITY(1,1) NOT NULL PRIMARY KEY,
  ID_MATERIAL INT NOT NULL,
  CANTIDAD_DESPERDICIADA DECIMAL(12,2) NOT NULL CONSTRAINT DF_DM_CANT_DESP DEFAULT (0),
  REUTILIZABLE CHAR(1) NOT NULL CONSTRAINT DF_DM_REU DEFAULT ('N'),
  CANTIDAD_REUTILIZADA DECIMAL(12,2) NOT NULL CONSTRAINT DF_DM_CANT_REU DEFAULT (0),
  MOTIVO NVARCHAR(250) NULL,
  FECHA DATETIME2(0) NOT NULL CONSTRAINT DF_DM_FECHA DEFAULT (GETDATE()),
  ID_USUARIO INT NULL,

  CONSTRAINT CK_DM_CANT_DESP CHECK (CANTIDAD_DESPERDICIADA >= 0),
  CONSTRAINT CK_DM_CANT_REU CHECK (CANTIDAD_REUTILIZADA >= 0),
  CONSTRAINT CK_DM_REU CHECK (REUTILIZABLE IN ('S','N')),

  CONSTRAINT FK_DM_MAT FOREIGN KEY (ID_MATERIAL) REFERENCES dbo.MATERIALES(ID_MATERIAL),
  CONSTRAINT FK_DM_USU FOREIGN KEY (ID_USUARIO) REFERENCES dbo.USUARIOS(ID_USUARIO)
);
GO
